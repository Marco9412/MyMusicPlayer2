apply plugin: 'com.android.application'

android {
    def getGitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    def getGitBranchName = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'status'
            standardOutput = stdout
        }
        // First line is "On branch xxxx", we take only the xxx part!
        return stdout.toString().split("\n")[0].substring(10)
    }

    compileSdkVersion 28
    //buildToolsVersion "28.0.2"

    defaultConfig {
        applicationId "com.panni.mymusicplayer2"
        minSdkVersion 21
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "GitHash", "\"${getGitHash()}\""
        buildConfigField "String", "GitBranchName", "\"${getGitBranchName()}\""
        buildConfigField "java.util.Date", "BUILD_DATE", "new java.util.Date(" + System.currentTimeMillis() + "L)"
    }

    buildTypes {
        // TODO -> cannot enable minify! Proguard deletes contextmenu code
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'

    implementation "com.android.support:mediarouter-v7:28.0.0"
    implementation 'com.android.support:support-compat:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'

    // Cast companion library
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.google.android.libraries.cast.companionlibrary:ccl:2.8.4'

    // ExoPlayer
    implementation 'com.google.android.exoplayer:exoplayer:r1.5.9'
    //implementation project(':library-release_1.5.9')
    //implementation project(':extension-flac-release_1.5.9')

    // Gif viewer
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.1.17'

    // Retrofit http client http://square.github.io/retrofit/
    //implementation 'com.squareup.retrofit2:retrofit:2.1.0'
    //implementation 'com.squareup.retrofit2:converter-gson:2.1.0'

    // okhttp
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
}